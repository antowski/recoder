
#Использовать asserts
#Использовать fs
#Использовать logos

Перем Лог;

Процедура ПерекодироватьФайл(Знач пИмяФайла) Экспорт

	Ожидаем.Что(ФС.ФайлСуществует(пИмяФайла), "Не найден файл <" + пИмяФайла + ">").ЭтоИстина();

	Если ЭтоФайлВUTF8withBOM(пИмяФайла) Тогда
		Возврат;
	КонецЕсли;

    ИсходнаяКодировка = "windows-1251";
    ЦелеваяКодировка = "UTF-8";

    // читаем в исходной кодировке
    ТекстовоеСодержимое = ПрочестьТекстФайлаВКодировке(пИмяФайла, ИсходнаяКодировка);
	
    // записываем в целевой кодировке
	ЗаписатьТекстВКодировке(ТекстовоеСодержимое, пИмяФайла, ЦелеваяКодировка);
	
КонецПроцедуры

// МаскаПоиска должна содержать расширения обрабатываемых файлов с ведущей точкой, перечисленные через запятую.
// Например: ".txt,.log"
Процедура ПерекодироватьКаталог(Знач ИмяКаталога, Знач МаскаПоиска = "") Экспорт
	
	Ожидаем.Что(ФС.КаталогСуществует(ИмяКаталога), "Не найден каталог <" + ИмяКаталога + ">").ЭтоИстина();
	Лог.Отладка("Выполняется перекодировка каталога <" + ИмяКаталога + ">");

	НайденныеФайлы = НайтиФайлы(ИмяКаталога, "*", Истина);

	Если ПустаяСтрока(МаскаПоиска) Тогда
		МассивОбрабатываемыхРасширений = Новый Массив;
	Иначе
		МассивОбрабатываемыхРасширений = СтрРазделить(МаскаПоиска, ",");
	КонецЕсли;
	
	Для каждого НайденныйФайл Из НайденныеФайлы Цикл

		Если НайденныйФайл.ЭтоКаталог() Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбрабатыватьФайл(НайденныйФайл, МассивОбрабатываемыхРасширений) Тогда
			Лог.Отладка("Обрабатываю файл: " + НайденныйФайл.ПолноеИмя);
			ПерекодироватьФайл(НайденныйФайл.ПолноеИмя);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ОбрабатыватьФайл(Файл, МассивОбрабатываемыхРасширений)

	Если МассивОбрабатываемыхРасширений.Количество() = 0 Тогда
		Возврат Истина;
	Иначе
		Возврат НЕ МассивОбрабатываемыхРасширений.Найти(Файл.Расширение) = Неопределено;
	КонецЕсли;

КонецФункции

Функция ПрочестьТекстФайлаВКодировке(пИмяФайла, пКодировкаСтрокой)

	Перем Читатель;

	Читатель = Новый ЧтениеТекста(пИмяФайла, пКодировкаСтрокой);
    ТекстовоеСодержимое = Читатель.Прочитать();
	Читатель.Закрыть();

	Возврат ТекстовоеСодержимое;

КонецФункции

Процедура ЗаписатьТекстВКодировке(пТекст, пИмяФайла, пКодировкаСтрокой)

	Перем Писатель;

	Если ПустаяСтрока(пТекст) Тогда
		Возврат;
	КонецЕсли;

	Писатель = Новый ЗаписьТекста(пИмяФайла, пКодировкаСтрокой);

	Попытка
		Писатель.Записать(пТекст);
	Исключение		
		Писатель.Закрыть();
		ВызватьИсключение "Ошибка при записи файла <" + пИмяФайла + ">: " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;	
	
	Писатель.Закрыть();

КонецПроцедуры

Функция ЭтоФайлВUTF8withBOM(ИмяФайла)
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
	ЧтениеДанных = Новый ЧтениеДанных(ДвоичныеДанные);
	БуферДанных = Новый БуферДвоичныхДанных(3);
	
	ЧтениеДанных.ПрочитатьВБуферДвоичныхДанных(БуферДанных,0,3);

	// BOM = 0xEFEEEF = 239 187 191
	ЕстьBOM = БуферДанных.Размер = 3 
		И БуферДанных[0] = 239 
		И БуферДанных[1] = 187 
		И БуферДанных[2] = 191;

	Возврат ЕстьBOM;
	
КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.lib.recoder");
//Лог.УстановитьУровень(УровниЛога.Отладка);
